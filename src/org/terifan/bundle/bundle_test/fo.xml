<?xml version="1.0" encoding="iso-8859-1"?>
<xsl:stylesheet version="1.0" xmlns:xsl="http://www.w3.org/1999/XSL/Transform" xmlns:java="http://xml.apache.org/xslt/java" xmlns:fo="http://www.w3.org/1999/XSL/Format">
<xsl:output method="xml" indent="no"/>

<xsl:template match="/">

	<fo:root xmlns:fo="http://www.w3.org/1999/XSL/Format">
		<fo:layout-master-set>
			<fo:simple-page-master margin-right="1.5cm" margin-left="1.5cm" margin-bottom="1cm" margin-top="0cm" page-width="21cm" page-height="29.7cm" master-name="first">
				<fo:region-body margin-top="1cm"/>
				<fo:region-before extent="1cm"/>
				<fo:region-after extent="1.5cm"/>
			</fo:simple-page-master>
		</fo:layout-master-set>

		<fo:page-sequence master-reference="first">
			<fo:flow flow-name="xsl-region-body">

				<xsl:for-each select="/xml/page">
					<xsl:variable name="page_number" select="position()"/>

					<fo:block break-after="page">

						<xsl:call-template name="page_header">
							<xsl:with-param name="page_number" select="$page_number"/>
						</xsl:call-template>

						<fo:block font-size="4.5pt" text-align="center">

							<fo:table table-layout="fixed" width="100%" border-collapse="collapse">
								<fo:table-column column-width="73pt"/>
								<fo:table-column column-width="73pt"/>
								<fo:table-column column-width="73pt"/>
								<fo:table-column column-width="73pt"/>
								<fo:table-column column-width="73pt"/>
								<fo:table-column column-width="73pt"/>
								<fo:table-column column-width="73pt"/>
								<fo:table-body height="100%">

									<xsl:call-template name="report_header">
										<xsl:with-param name="page_number" select="$page_number"/>
									</xsl:call-template>

									<xsl:call-template name="report_body">
										<xsl:with-param name="page_number" select="$page_number"/>
									</xsl:call-template>

									<fo:table-row>
										<fo:table-cell number-columns-spanned="7" border-top="0.5pt solid black"><fo:block></fo:block></fo:table-cell>
									</fo:table-row>

								</fo:table-body>
							</fo:table>
						</fo:block>

					</fo:block>

				</xsl:for-each>

			</fo:flow>
		</fo:page-sequence>
	</fo:root>

</xsl:template>


<xsl:template name="page_header">
	<xsl:param name="page_number"/>
	<xsl:variable name="page" select="/xml/page[$page_number]"/>

	<fo:block font-weight="bold">
		<fo:table table-layout="fixed" width="100%" border-collapse="collapse">
			<fo:table-column column-width="70%"/>
			<fo:table-column column-width="30%"/>

			<fo:table-body>
				<fo:table-row>
					<fo:table-cell>
						<fo:block>
							<xsl:choose>
								<xsl:when test="contains($page/subtitle, 'Sjuan')">
									<fo:external-graphic src="http://lokaltv.tv4.se/public/logos/sjuan.gif" content-height="10mm"/>
								</xsl:when>
								<xsl:otherwise>
									<fo:external-graphic src="http://lokaltv.tv4.se/public/logos/tv4.gif"/>
								</xsl:otherwise>
							</xsl:choose>
						</fo:block>
					</fo:table-cell>
					<fo:table-cell text-align="end" padding-before="3pt" padding-after="3pt" display-align="after">
						<fo:block>
							<xsl:value-of select="$page/title"/>
						</fo:block>
					</fo:table-cell>
				</fo:table-row>
			</fo:table-body>
		</fo:table>
	</fo:block>
</xsl:template>


<xsl:template name="report_header">
	<xsl:param name="page_number"/>
	<xsl:variable name="page" select="/xml/page[$page_number]"/>

	<fo:table-row font-weight="bold">
		<xsl:for-each select="$page/weekdays/weekday[index!='-1' and index!='7']">
			<fo:table-cell border="0.5pt solid black">
				<xsl:if test="index='5' or index='6'"><xsl:attribute name="color">#dd0000</xsl:attribute></xsl:if>

				<fo:block><xsl:value-of select="name"/></fo:block>
				<fo:block><xsl:value-of select="date"/></fo:block>
			</fo:table-cell>
		</xsl:for-each>
	</fo:table-row>
</xsl:template>


<xsl:template name="report_body">
	<xsl:param name="page_number"/>
	<xsl:variable name="page" select="/xml/page[$page_number]"/>

	<xsl:for-each select="/xml/sections/section">
		<xsl:variable name="section_start_time"><xsl:value-of select="start_time"/></xsl:variable>
		<xsl:variable name="section_end_time"><xsl:value-of select="end_time"/></xsl:variable>

		<xsl:if test="name">
			<xsl:if test="substring(name,1,5)='Prime'">
				<fo:table-row font-weight="bold">
					<fo:table-cell border-top="0.5pt solid black" number-columns-spanned="7" background-color="#aa0000">
						<fo:block page-break-after="always"/>
					</fo:table-cell>
				</fo:table-row>

				<xsl:call-template name="report_header">
					<xsl:with-param name="page_number" select="$page_number"/>
				</xsl:call-template>
			</xsl:if>
			<fo:table-row font-weight="bold">
				<fo:table-cell border="0.5pt solid black" number-columns-spanned="7" background-color="#aa0000">
					<fo:block><xsl:value-of select="name"/></fo:block>
				</fo:table-cell>
			</fo:table-row>
		</xsl:if>

		<xsl:if test="$page/blocks/block[weekday!='1' and weekday!='7' and (translate(time,':','') &gt;= $section_start_time) and (translate(time,':','') &lt; $section_end_time)]">
			<fo:table-row>

				<xsl:for-each select="$page/weekdays/weekday[index!='-1' and index!='7']">
					<xsl:variable name="weekday"><xsl:value-of select="index"/></xsl:variable>

					<fo:table-cell border-right="0.5pt solid black">
						<xsl:if test="$weekday='0'"><xsl:attribute name="border-left">0.5pt solid black</xsl:attribute></xsl:if>

						<xsl:choose>
							<xsl:when test="$page/blocks/block[weekday=$weekday and (translate(time,':','') &gt;= $section_start_time) and (translate(time,':','') &lt; $section_end_time)]">
								<fo:table table-layout="fixed" width="100%" border-collapse="collapse">
									<fo:table-column column-width="100%"/>
									<fo:table-body>

										<xsl:for-each select="$page/blocks/block[weekday=$weekday and (translate(time,':','') &gt;= $section_start_time) and (translate(time,':','') &lt; $section_end_time)]">
											<xsl:call-template name="single_report_item">
												<xsl:with-param name="page_number" select="$page_number"/>
											</xsl:call-template>
										</xsl:for-each>

									</fo:table-body>
								</fo:table>
							</xsl:when>
							<xsl:otherwise>
								<fo:block>
								</fo:block>
							</xsl:otherwise>
				 		</xsl:choose>

					</fo:table-cell>
				</xsl:for-each>
			</fo:table-row>
		</xsl:if>

	</xsl:for-each>
</xsl:template>


<xsl:template name="single_report_item">
	<xsl:param name="page_number"/>
	<xsl:variable name="page" select="/xml/page[$page_number]"/>

	<xsl:variable name="time"><xsl:value-of select="serial_time"/></xsl:variable>

	<xsl:variable name="program" select="$page/programs/program[($time &gt; serial_start_time) and ($time &lt;= serial_end_time)]"/>

	<fo:table-row>
		<fo:table-cell border-bottom="0.5pt solid black" border-top="0.5pt solid black">

			<xsl:choose>
				<xsl:when test="is_sumo='0'">
					<fo:block background-color="#ffcc00">
						<xsl:value-of select="time"/>
					</fo:block>
					<fo:block background-color="#ffcc00">
						<xsl:value-of select="$program/name"/>&#160;<xsl:value-of select="rating"/>
					</fo:block>
				</xsl:when>
				<xsl:otherwise>
					<fo:block background-color="#ff9900">
						<xsl:value-of select="time"/>
					</fo:block>
					<fo:block background-color="#ff9900">
						<xsl:value-of select="$program/name"/>&#160;<xsl:value-of select="rating"/>
					</fo:block>
				</xsl:otherwise>
			</xsl:choose>

		</fo:table-cell>
	</fo:table-row>
</xsl:template>

</xsl:stylesheet>